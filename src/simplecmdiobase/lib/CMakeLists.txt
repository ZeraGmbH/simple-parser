set(CMAKE_INCLUDE_CURRENT_DIR ON)

set(HEADERS
    cmdparserbase.h
    cmdclient.h
    cmdhandlerbase.h
    cmdserver.h
    QSimpleCmdHandlerBase
    QSimpleCmdParserSocketBase
    QSimpleCmdServer
)

set(SOURCES
    cmdclient.cpp
    cmdhandlerbase.cpp
    cmdparserbase.cpp
    cmdserver.cpp
)

add_library(simplecmdiobase SHARED
    ${HEADERS}
    ${SOURCES}
)

set_target_properties(simplecmdiobase PROPERTIES VERSION ${PROJECT_VERSION})
set_target_properties(simplecmdiobase PROPERTIES SOVERSION ${PROJECT_VERSION_MAJOR})

target_link_libraries (simplecmdiobase
    PUBLIC
    Qt5::Core
    Qt5::Network
    # ask linker to help us finding unresolved symbols
    "-Wl,--no-undefined"
)

target_include_directories(simplecmdiobase
    PUBLIC
    $<BUILD_INTERFACE:${CMAKE_CURRENT_SOURCE_DIR}>
    $<BUILD_INTERFACE:${CMAKE_CURRENT_BINARY_DIR}>
    $<INSTALL_INTERFACE:${CMAKE_INSTALL_INCLUDEDIR}/simplecmdiobase>
)

include(GenerateExportHeader)
generate_export_header(simplecmdiobase)

install(FILES
    ${CMAKE_CURRENT_BINARY_DIR}/simplecmdiobase_export.h
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/simplecmdiobase
    )

install(TARGETS simplecmdiobase
    EXPORT simplecmdiobase
    LIBRARY DESTINATION ${CMAKE_INSTALL_LIBDIR}
)

install(
    FILES ${HEADERS}
    DESTINATION ${CMAKE_INSTALL_INCLUDEDIR}/simplecmdiobase
)
